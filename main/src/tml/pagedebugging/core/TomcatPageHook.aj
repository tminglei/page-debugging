package tml.pagedebugging.core;

/**
 * Insert pagedebugging code at the begin and end of the requested resources (file) to help debugging
 * (compatible with Tomcat 5.5/6.0, JBoss-Web 2)
 * 
 * @author tminglei
 */
public privileged aspect TomcatPageHook
{

	/**
	 * >> Tomcat: insert pagedebugging code at first and last of the content generated by the dynamically
	 * requested resources
	 */
	void around(org.apache.catalina.core.ApplicationDispatcher resourceDispather, 
			
			javax.servlet.ServletResponse response) 
	
			throws java.io.IOException : 
					
		//-- pointcuts
		this( resourceDispather ) && args( *, response ) 
		
			&& call( void org.apache.catalina.core.ApplicationFilterChain.doFilter(..) )
			
			&& withincode( void invoke(..) ) 
			
	{	//-- operations
		
		PageDebuggingContext ctx = PageDebuggingContext.currentContext();
		
    	String fileName = resourceDispather.requestURI;
    	
		if( ctx.debug() && ctx.allow( fileName ) )
		{
			try 
			{
				ctx.enter( fileName );
				
		    	response.getWriter().append( "\n<!-- " +ctx.currentDebuggingCode()+ " - start -->\n" );
		    	//--
		    	proceed( resourceDispather, response );
		    	//--
		    	response.getWriter().append( "\n<!-- " +ctx.currentDebuggingCode()+ " - end -->\n" );
			}
			finally
			{
				ctx.exit();
			}
		}
		else
		{
	    	proceed( resourceDispather, response );  
		}
		
	}
	
}
